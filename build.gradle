plugins {
    id 'com.palantir.git-version' version '0.15.0'
    id 'org.jetbrains.kotlin.jvm' version '1.7.20'
}

group 'net.transgressoft'
version gitVersion()

gradle.projectsEvaluated {
    tasks.withType(JavaCompile){
        options.compilerArgs << "-Xlint:unchecked"
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
}

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'jacoco'
    apply plugin: 'kotlin'
    apply plugin: 'maven-publish'

    ext {
        kotlinVersion = '1.7.20'

        // the password needs to be specified via command line
        repoPass = project.hasProperty('repoPass') ? project.getProperty('repoPass') : ''
    }

    dependencies {
        implementation 'com.neovisionaries:nv-i18n:1.29'
        implementation 'net.transgressoft:soft-query-api:0.53-SNAPSHOT'
        implementation platform('org.jetbrains.kotlin:kotlin-bom')
        implementation "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"
    }

    repositories {
        mavenCentral()
        maven {
            url 'https://nexus.transgressoft.net/repository/maven-public/'
        }
    }

    configurations.all {
        resolutionStrategy {
            cacheChangingModulesFor 0, 'seconds'
        }
    }

    plugins.withType(JacocoPlugin) {
        tasks['test'].finalizedBy 'jacocoTestReport'
    }

    jacocoTestReport {
        reports {
            xml.required.set true
        }
    }

    task sourceJar(type: Jar, dependsOn: classes) {
        classifier 'sources'
        from sourceSets.main.allSource
        from sourceSets.main.output
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier 'javadoc'
        from javadoc.destinationDir
        javadoc.options.addStringOption('Xdoclint:none', '-quiet')
    }

    compileKotlin {
        kotlinOptions {
            jvmTarget = JavaVersion.VERSION_11
            freeCompilerArgs += '-Xjvm-default=all'
        }
    }

    compileTestKotlin {
        kotlinOptions {
            jvmTarget = JavaVersion.VERSION_11
        }
    }

    test {
        useJUnitPlatform()
        testLogging {
            events 'passed', 'skipped', 'failed'
        }
    }
}

project('music-commons-api') {

    compileJava {
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11
    }
}

project('music-commons-core') {

    compileJava {
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11
    }

    configurations {
        spi
    }

    task spiJar(type: Jar) {
        archiveBaseName = 'music-commons-core-spi'
        from sourceSets.main.output
        include('com/transgressoft/music/**')
    }

    artifacts {
        spi spiJar
    }

    dependencies {
        api project(path: ':music-commons-api')
    }
}

import org.gradle.api.JavaVersion

plugins {
    id 'com.palantir.git-version' version '0.15.0'
    id 'org.jetbrains.kotlin.jvm' version '1.7.0'
}

group 'net.transgressoft'
version gitVersion()

gradle.projectsEvaluated {
    tasks.withType(JavaCompile){
        options.compilerArgs << "-Xlint:unchecked"
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'java-library'
    apply plugin: 'jacoco'
    apply plugin: 'kotlin'
    apply plugin: 'maven-publish'

    ext {
        kotlinVersion = '1.7.0'

        // the password needs to be specified via command line
        repoPass = project.hasProperty('repoPass') ? project.getProperty('repoPass') : ''
    }

    dependencies {
        implementation 'net.transgressoft:soft-query-api:0.29+'
        implementation platform('org.jetbrains.kotlin:kotlin-bom')
        implementation "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"
    }

    repositories {
        mavenCentral()
        maven {
            url 'http://nexus.pipon.xxx/repository/maven-public/'
            allowInsecureProtocol true
        }
    }

    configurations.all {
        resolutionStrategy {
            cacheChangingModulesFor 0, 'seconds'
        }
    }

    plugins.withType(JacocoPlugin) {
        tasks['test'].finalizedBy 'jacocoTestReport'
    }

    jacocoTestReport {
        reports {
            xml.required.set true
        }
    }

    task sourceJar(type: Jar, dependsOn: classes) {
        classifier 'sources'
        from sourceSets.main.allSource
        from sourceSets.main.output
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier 'javadoc'
        from javadoc.destinationDir
        javadoc.options.addStringOption('Xdoclint:none', '-quiet')
    }

    compileKotlin {
        kotlinOptions {
            jvmTarget = JavaVersion.VERSION_1_8
            freeCompilerArgs += '-Xjvm-default=enable'
        }
    }

    compileTestKotlin {
        kotlinOptions {
            jvmTarget = JavaVersion.VERSION_1_8
        }
    }

    test {
        useJUnitPlatform()
        testLogging {
            events 'passed', 'skipped', 'failed'
        }
    }
}

project('music-commons-api') {

    compileJava {
        sourceCompatibility = JavaVersion.VERSION_16
        targetCompatibility = JavaVersion.VERSION_16
    }
}

project('music-commons-core') {

    compileJava {
        sourceCompatibility = JavaVersion.VERSION_16
        targetCompatibility = JavaVersion.VERSION_16
    }

    configurations {
        spi
    }

    task spiJar(type: Jar) {
        archiveBaseName = 'music-commons-core-spi'
        from sourceSets.main.output
        include('com/transgressoft/music/**')
    }

    artifacts {
        spi spiJar
    }

    dependencies {
        api project(path: ':music-commons-api')
    }
}
